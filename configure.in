# Source of configuration for Kyoto Tycoon



#================================================================
# Generic Settings
#================================================================

# Package name
AC_INIT(kyototycoon, 0.9.3)

# Package information
MYLIBVER=1
MYLIBREV=9

# Targets
MYHEADERFILES="ktcommon.h ktutil.h ktsocket.h ktthserv.h kthttp.h ktrpc.h"
MYHEADERFILES="$MYHEADERFILES kttimeddb.h ktremotedb.h"
MYLIBRARYFILES="libkyototycoon.a"
MYLIBOBJFILES="ktutil.o ktsocket.o ktthserv.o kthttp.o ktrpc.o"
MYLIBOBJFILES="$MYLIBOBJFILES kttimeddb.o ktremotedb.o"
MYCOMMANDFILES="ktutiltest ktutilmgr ktutilserv kttimedtest kttimedmgr"
MYCOMMANDFILES="$MYCOMMANDFILES ktserver ktremotetest ktremotemgr"
MYMAN1FILES="ktutiltest.1 ktutilmgr.1 ktutilserv.1 kttimedtest.1 kttimedmgr.1"
MYMAN1FILES="$MYMAN1FILES ktserver.1 ktremotetest.1 ktremotemgr.1"
MYDOCUMENTFILES="COPYING ChangeLog doc kyototycoon.idl"
MYPCFILES="kyototycoon.pc"

# Building flags
MYCFLAGS="-Wall -ansi -pedantic -fPIC -fsigned-char -O2"
MYCXXFLAGS="-Wall -fPIC -fsigned-char -O2"
MYCPPFLAGS="-I. -I\$(INCLUDEDIR) -I$HOME/include -I/usr/local/include"
MYCPPFLAGS="$MYCPPFLAGS -DNDEBUG -D_GNU_SOURCE=1"
MYCPPFLAGS="$MYCPPFLAGS -D_FILE_OFFSET_BITS=64 -D_REENTRANT -D__EXTENSIONS__"
MYLDFLAGS="-L. -L\$(LIBDIR) -L$HOME/lib -L/usr/local/lib"
MYCMDLDFLAGS=""
MYRUNPATH="\$(LIBDIR)"
MYLDLIBPATHENV="LD_LIBRARY_PATH"
MYPOSTCMD="true"

# Building paths
PATH="$PATH:$HOME/bin:/usr/local/bin:/usr/local/sbin:/usr/bin:/usr/sbin:/bin:/sbin"
PATH="$PATH:/opt/SUNWspro/bin:/usr/ccs/bin:/usr/xpg4/bin:/usr/xpg6/bin:/usr/ucb"
CPATH="$HOME/include:/usr/local/include:$CPATH"
LIBRARY_PATH="$HOME/lib:/usr/local/lib:$LIBRARY_PATH"
LD_LIBRARY_PATH="$HOME/lib:/usr/local/lib:$LD_LIBRARY_PATH"
PKG_CONFIG_PATH="$HOME/lib/pkgconfig:/usr/local/lib/pkgconfig:$PKG_CONFIG_PATH"
export PATH CPATH LIBRARY_PATH LD_LIBRARY_PATH PKG_CONFIG_PATH



#================================================================
# Options
#================================================================

# Internal variables
enables=""

# Debug mode
AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug], [build for debugging]))
if test "$enable_debug" = "yes"
then
  MYCFLAGS="-Wall -ansi -pedantic -fPIC -fsigned-char -O0"
  MYCXXFLAGS="-Wall -fPIC -fsigned-char -g -O0"
  MYCPPFLAGS="$MYCPPFLAGS -UNDEBUG -D_KCDEBUG"
  MYCMDLDFLAGS="$MYCMDLDFLAGS -static"
  MYRUNPATH="$MYRUNPATH:."
  enables="$enables (debug)"
fi

# Developping mode
AC_ARG_ENABLE(devel,
  AC_HELP_STRING([--enable-devel], [build for development]))
if test "$enable_devel" = "yes"
then
  MYCFLAGS="-Wall -ansi -pedantic -fPIC -fsigned-char -O2 -fno-inline -pipe"
  MYCXXFLAGS="-Wall -fPIC -fsigned-char -g -O2 -fno-inline -pipe"
  MYCPPFLAGS="$MYCPPFLAGS -UNDEBUG -D_KCDEBUG"
  MYRUNPATH="$MYRUNPATH:."
  enables="$enables (devel)"
fi

# Profiling mode
AC_ARG_ENABLE(profile,
  AC_HELP_STRING([--enable-profile], [build for profiling]))
if test "$enable_profile" = "yes"
then
  MYCXXFLAGS="-Wall -fPIC -fsigned-char -g -pg -O2 -fno-inline -pipe"
  MYRUNPATH="$MYRUNPATH:."
  enables="$enables (profile)"
fi

# Micro yield mode
AC_ARG_ENABLE(uyield,
  AC_HELP_STRING([--enable-uyield], [build for detecting race conditions]))
if test "$enable_uyield" = "yes"
then
  MYCPPFLAGS="$MYCPPFLAGS -UNDEBUG -D_KCUYIELD"
  MYRUNPATH="$MYRUNPATH:."
  enables="$enables (uyield)"
fi

# Static mode
AC_ARG_ENABLE(static,
  AC_HELP_STRING([--enable-static], [build by static linking]))
if test "$enable_static" = "yes"
then
  MYCMDLDFLAGS="$MYCMDLDFLAGS -static"
  enables="$enables (static)"
fi

# Disable shared object
AC_ARG_ENABLE(shared,
  AC_HELP_STRING([--disable-shared], [avoid to build shared libraries]))
if test "$enable_shared" = "no"
then
  enables="$enables (no-shared)"
fi

# Disable specific event notifiers
AC_ARG_ENABLE(event,
  AC_HELP_STRING([--disable-event], [avoid to use system-specific event notifiers]))
if test "$enable_event" = "no"
then
  MYCPPFLAGS="$MYCPPFLAGS -D_MYNOEVENT"
  enables="$enables (no-event)"
fi

# Enable Lua extension
AC_ARG_ENABLE(lua,
  AC_HELP_STRING([--enable-lua], [build with Lua extension]))
if test "$enable_lua" = "yes"
then
  luaver=`lua -e 'v = string.gsub(_VERSION, ".* ", ""); print(v)'`
  MYCPPFLAGS="$MYCPPFLAGS -D_MYLUA"
  MYCPPFLAGS="$MYCPPFLAGS -I/usr/include/lua$luaver -I/usr/local/include/lua$luaver"
  MYCPPFLAGS="$MYCPPFLAGS -I/usr/include/lua -I/usr/local/include/lua"
  MYLDFLAGS="$MYLDFLAGS -L/usr/lib/lua$luaver -L/usr/local/lib/lua$luaver"
  MYLDFLAGS="$MYLDFLAGS -L/usr/lib/lua -L/usr/local/lib/lua"
  LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/usr/lib/lua$luaver:/usr/local/lib/lua$luaver"
  LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/usr/lib/lua:/usr/local/lib/lua"
  LIBRARY_PATH="$LIBRARY_PATH:/usr/lib/lua$luaver:/usr/local/lib/lua$luaver"
  LIBRARY_PATH="$LIBRARY_PATH:/usr/lib/lua:/usr/local/lib/lua"
  CPATH="$CPATH:/usr/include/lua$luaver:/usr/local/include/lua$luaver"
  CPATH="$CPATH:/usr/include/lua:/usr/local/include/lua"
  enables="$enables (lua)"
fi

# Specify the installation path of Kyoto Cabinet
AC_ARG_WITH(kc,
  AC_HELP_STRING([--with-kc=DIR], [search DIR/include and DIR/lib for Kyoto Cabinet]))
if test -n "$with_kc"
then
  MYCPPFLAGS="$MYCPPFLAGS -I$with_kc/include"
  MYLDFLAGS="$MYLDFLAGS -L$with_kc/lib"
  MYRUNPATH="$MYRUNPATH:$with_kc/lib"
  CPATH="$CPATH:$with_kc/include"
  LIBRARY_PATH="$LIBRARY_PATH:$with_kc/lib"
  LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$with_kc/lib"
  PKG_CONFIG_PATH="$PKG_CONFIG_PATH:$with_kc/lib/pkgconfig"
fi

# Specify the installation path of Lua
AC_ARG_WITH(lua,
  AC_HELP_STRING([--with-lua=DIR], [search DIR/include and DIR/lib for Lua]))
if test -n "$with_lua"
then
  MYCPPFLAGS="$MYCPPFLAGS -I$with_lua/include"
  MYLDFLAGS="$MYLDFLAGS -L$with_lua/lib"
  MYRUNPATH="$MYRUNPATH:$with_lua/lib"
  CPATH="$CPATH:$with_lua/include"
  LIBRARY_PATH="$LIBRARY_PATH:$with_lua/lib"
  LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$with_lua/lib"
  PKG_CONFIG_PATH="$PKG_CONFIG_PATH:$with_lua/lib/pkgconfig"
fi

# Messages
printf '#================================================================\n'
printf '# Configuring Kyoto Tycoon version %s%s.\n' "$PACKAGE_VERSION" "$enables"
printf '#================================================================\n'



#================================================================
# Checking Commands and Libraries
#================================================================

# C and C++ compilers
AC_PROG_CC
AC_PROG_CXX
AC_LANG(C++)
if test "$enable_devel" = "yes"
then
  AC_CHECK_PROG(is_ccache, ccache, yes, no)
  if test "$is_ccache" = "yes"
  then
    CC="ccache $CC"
    CXX="ccache $CXX"
    ccache -C >config.tmp 2>&1
  fi
fi

# Reset variables
if test "$GCC" != "yes"
then
  AC_MSG_WARN([another compiler except for GCC was detected])
  MYCXXFLAGS=""
fi
test -n "$CXXFLAGS" && MYCXXFLAGS="$CXXFLAGS $MYCXXFLAGS"
test -n "$CPPFLAGS" && MYCPPFLAGS="$CPPFLAGS $MYCPPFLAGS"
test -n "$LDFLAGS" && MYLDFLAGS="$LDFLAGS $MYLDFLAGS"

# System-depending optimization
printf 'checking for 64-bit availability... '
if printf 'main() {}' | $CC -xc -m64 -o config.tmp - >config.tmp 2>&1
then
  MYCFLAGS="-m64 $MYCFLAGS"
  MYCXXFLAGS="-m64 $MYCXXFLAGS"
  printf 'yes\n'
else
  printf 'no\n'
fi
printf 'checking whether the CPU is i686... '
if uname -a | egrep ' i686' >config.tmp
then
  MYCXXFLAGS="-march=i686 $MYCXXFLAGS"
  printf 'yes\n'
else
  printf 'no\n'
fi

# Underlying libraries
AC_CHECK_LIB(c, main)
AC_CHECK_LIB(m, main)
AC_CHECK_LIB(pthread, main)
AC_CHECK_LIB(rt, main)
AC_CHECK_LIB(dl, main)
AC_CHECK_LIB(nsl, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(resolv, main)
AC_CHECK_LIB(stdc++, main)
AC_CHECK_LIB(z, main)
AC_CHECK_LIB(lzo2, main)
AC_CHECK_LIB(lzma, main)
AC_CHECK_LIB(kyotocabinet, main)
if test "$enable_lua" = "yes"
then
  AC_CHECK_LIB(lua, main)
  AC_CHECK_LIB(lua$luaver, main)
fi
AC_CHECK_LIB(kyototycoon, main, AC_MSG_WARN([old version of Kyoto Tycoon was detected]))

# Necessary headers
AC_CHECK_HEADER(stdlib.h, true, AC_MSG_ERROR([stdlib.h is required]))
AC_CHECK_HEADER(stdint.h, true, AC_MSG_ERROR([stdint.h is required]))
AC_CHECK_HEADER(unistd.h, true, AC_MSG_ERROR([unistd.h is required]))
AC_CHECK_HEADER(sys/socket.h, true, AC_MSG_ERROR([sys/socket.h is required]))
AC_CHECK_HEADER(kccommon.h, true, AC_MSG_ERROR([kccommon.h is required]))
if test "$enable_lua" = "yes"
then
  AC_CHECK_HEADER(lua.h, true, AC_MSG_ERROR([lua.h is required]))
fi

# Checking the version of Kyoto Cabinet with pkg-config
if type pkg-config >config.tmp 2>&1
then
  printf 'checking Kyoto Cabinet by pkg-config... '
  if pkg-config --atleast-version=1.2.17 kyotocabinet
  then
    printf 'yes\n'
  else
    printf 'no\n'
    AC_MSG_ERROR([required version of Kyoto Cabinet was not detected])
  fi
fi

# Shared libraries
if test "$enable_shared" != "no" && test "$enable_profile" != "yes"
then
  if uname | grep Darwin >config.tmp
  then
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.$MYLIBVER.$MYLIBREV.0.dylib"
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.$MYLIBVER.dylib"
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.dylib"
    MYLDLIBPATHENV="DYLD_LIBRARY_PATH"
  else
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.so.$MYLIBVER.$MYLIBREV.0"
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.so.$MYLIBVER"
    MYLIBRARYFILES="$MYLIBRARYFILES libkyototycoon.so"
  fi
fi

# Work around of bugs of some environments
if uname | grep Darwin >config.tmp
then
  MYCFLAGS="$MYCFLAGS -Os"
  MYCXXFLAGS="$MYCXXFLAGS -Os"
fi



#================================================================
# Generic Settings
#================================================================

# Export variables
AC_SUBST(MYLIBVER)
AC_SUBST(MYLIBREV)
AC_SUBST(MYFORMATVER)
AC_SUBST(MYHEADERFILES)
AC_SUBST(MYLIBRARYFILES)
AC_SUBST(MYLIBOBJFILES)
AC_SUBST(MYCOMMANDFILES)
AC_SUBST(MYMAN1FILES)
AC_SUBST(MYDOCUMENTFILES)
AC_SUBST(MYPCFILES)
AC_SUBST(MYCFLAGS)
AC_SUBST(MYCXXFLAGS)
AC_SUBST(MYCPPFLAGS)
AC_SUBST(MYLDFLAGS)
AC_SUBST(MYCMDLDFLAGS)
AC_SUBST(MYRUNPATH)
AC_SUBST(MYLDLIBPATHENV)
AC_SUBST(MYPOSTCMD)

# Targets
AC_OUTPUT(Makefile kyototycoon.pc)

# Messages
printf '#================================================================\n'
printf '# Ready to make.\n'
printf '#================================================================\n'



# END OF FILE
